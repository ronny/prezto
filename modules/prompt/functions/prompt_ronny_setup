#
# Ronny Haryanto's preferred prompt theme, adapted from Sorin Ionescu's theme.
#
# Authors:
#   Ronny Haryanto <ronny@haryan.to>
#   Sorin Ionescu <sorin.ionescu@gmail.com> (please don't bother Sorin with bug reports for this theme)
#
# Screenshots:
#   http://
#

# Load dependencies.
pmodload 'helper'

local node_prompt_part

function prompt_ronny_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_ronny_pwd="$MATCH"
    unset MATCH
  else
    _prompt_ronny_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi
}

function prompt_ronny_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_ronny_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # if (( $+functions[ruby-info] )); then
  #   ruby-info
  # fi

  if [[ -e .nvmrc || -e .node-version ]]; then
    if (( $+functions[node-info] )); then
      node-info
      node_prompt_part=" v${node_info[version]#version:} "
    fi
  else
    node_prompt_part=""
  fi
}

function prompt_ronny_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_ronny_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%B%F{yellow}»%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format '%F{red}♺%f '
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%B«%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'

  zstyle ':prezto:module:git:info:branch' format '%F{yellow}%b%f'
  zstyle ':prezto:module:git:info:commit' format '%F{red}%.7c%f'
  # position e.g. `master~1`
  zstyle ':prezto:module:git:info:position' format '%F{red}%p%f'
  zstyle ':prezto:module:git:info:action' format '%%B%F{yellow}%s%f%%b'

  zstyle ':prezto:module:git:info:added' format '%%B%F{magenta}+%f %%b'
  zstyle ':prezto:module:git:info:ahead' format '%%B%F{yellow}⇡%f %%b'
  zstyle ':prezto:module:git:info:behind' format '%%B%F{yellow}⇣%f %%b'
  zstyle ':prezto:module:git:info:deleted' format '%%B%F{magenta}☓%f %%b'
  zstyle ':prezto:module:git:info:modified' format '%%B%F{magenta}✳%f %%b'
  zstyle ':prezto:module:git:info:renamed' format '%%B%F{magenta}⇢%f %%b'
  zstyle ':prezto:module:git:info:stashed' format '%%B%F{cyan}⦿%f %%b'
  zstyle ':prezto:module:git:info:unmerged' format '%%B%F{magenta}U%f %%b'
  zstyle ':prezto:module:git:info:untracked' format '%%B%F{magenta}?%f %%b'

  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' [$(coalesce "%b" "%c" "%p")]%A%B%S%a%d%m%r%U%u%s'

  # %v - ruby version.
  #zstyle ':prezto:module:ruby:info:version' format ' ruby-%v'

  # Define prompts.
  #PROMPT='%(!.%F{red}.%F{green}%n@%m)%f:%F{blue}${_prompt_ronny_pwd}%f${git_info:+${(e)git_info[prompt]}}%(!.%B%F{red}#%f%b.)${node_prompt_part}${editor_info[keymap]} '
  PROMPT='%F{blue}${_prompt_ronny_pwd}%f${git_info:+${(e)git_info[prompt]}}%(!.%B%F{red}#%f%b.)${node_prompt_part}${editor_info[keymap]} '
  RPROMPT='${editor_info[overwrite]}%(?:: %F{red}⏎%f)${VIM:+" %B%F{green}V%f%b"}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_ronny_setup "$@"

